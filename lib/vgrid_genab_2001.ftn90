!-------------------------------------- LICENCE BEGIN ------------------------------------
!Environment Canada - Atmospheric Science and Technology License/Disclaimer, 
!                     version 3; Last Modified: May 7, 2008.
!This is free but copyrighted software; you can use/redistribute/modify it under the terms 
!of the Environment Canada - Atmospheric Science and Technology License/Disclaimer 
!version 3 or (at your option) any later version that should be found at: 
!http://collaboration.cmc.ec.gc.ca/science/rpn.comm/license.html 
!
!This software is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; 
!without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE. 
!See the above mentioned License/Disclaimer for more details.
!You should have received a copy of the License/Disclaimer along with this software; 
!if not, you can write to: EC-RPN COMM Group, 2121 TransCanada, suite 500, Dorval (Quebec), 
!CANADA, H9P 1J3; or send e-mail to service.rpn@ec.gc.ca
!-------------------------------------- LICENCE END --------------------------------------
module vdescript_2001

  implicit none
  private

  ! Define class variables
      
  ! Define and publicize interfaces
  public :: vgrid_genab_2001
  interface reallocate
     module procedure PRIV_reallocate4
     module procedure PRIV_reallocate8
     module procedure PRIV_reallocateI
  end interface

#include "vgrid.hf"

contains
   
   subroutine vgrid_genab_2001 (F_pres,F_a_8,F_b_8,F_err,ip1)
      
      !    Using pres coordinate generate A=0 and B=convip(...,F_pres,...)
      
      implicit none
      !      
      ! Arguments
      integer, intent(out) :: F_err
      real, dimension(:), intent(in) :: F_pres    ! user specification for vertical layering eta
      real*8, dimension(:), pointer :: F_a_8, F_b_8 ! model As and Bs
      integer, dimension(:), pointer, optional :: ip1 ! ip1 value

      ! Local variables
      integer :: k,i, status, nk
      logical :: monotone=.true.,wrongsig=.false.
      character*16 dumc_S

      !     __________________________________________________________________
      
      F_err = VGD_ERROR

      ! Set size of the problem
      nk = size(F_pres)
      
      ! Check and allocate as required
      status = reallocate(F_a_8,1,nk)
      if (status /= VGD_OK) return
      status = reallocate(F_b_8,1,nk)
      if (status /= VGD_OK) return
      !
      if(F_pres(1).eq.0.d0)then
         wrongsig = .true.
         write(for_msg,1000)
         call msg(MSG_ERROR,VGD_PRFX//for_msg)
      endif
      do k=2, nk
         if (F_pres(k).le.F_pres(k-1))monotone = .false.
      end do
      if(.not.monotone)then
         wrongsig = .true.
         write(for_msg,1003)
         call msg(MSG_ERROR,VGD_PRFX//for_msg)
      endif
      if (wrongsig) then
         write(for_msg,1002)
         call msg(MSG_ERROR,VGD_PRFX//for_msg)
         do k=1, nk
            write(for_msg,*) F_pres(k),k
            call msg(MSG_ERROR,VGD_PRFX//for_msg)               
         end do
         return
      endif
      !
      do k=1,nk
         F_b_8(k)=0.0
         F_a_8(k)=F_pres(k)*100.d0
      end do
      !
      if(present(ip1))then
         status = reallocate(ip1,1,nk)
         if (status /= VGD_OK) return
         do k=1,nk
            call convip(ip1(k),F_pres(k),2,2,dumc_S,.false.)
         enddo
      endif
      !
      F_err = VGD_OK
      !
      return

1000  format (/' ===> WRONG SPECIFICATION OF PRESSURE VERTICAL LEVELS:'/&
           '      eta(NK) MUST BE GREATER THAN 0.0')
      
1003  format (/' ===> WRONG SPECIFICATION OF PRESSURE VERTICAL LEVELS:'/&
           '      LEVELS MUST BE MONOTONICALLY INCREASING')
1002  format (/'      Current choice:')

   end subroutine vgrid_genab_2001
   
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
 function PRIV_reallocate4(v,low,high) result(status)
    ! Allocate space for outputs
    real, dimension(:), pointer :: v            !vector to allocate
    integer, intent(in) :: low,high             !bounds for vector
    integer :: status                           !return status of function
    
    ! Internal variables
    integer :: check

    ! Deallocate and re-allocate with the correct size
    status = VGD_ERROR
    if (associated(v)) then
       deallocate(v,stat=check)
       if (check /= 0) then
          write(for_msg,*) 'in deallocate() from vdescript_2001::reallocate()'
          call msg(MSG_ERROR,VGD_PRFX//for_msg)          
          return
       endif
    endif
    allocate(v(low:high),stat=check)
    if (check /= 0) then
       write(for_msg,*) 'in allocate() from vdescript_2001::reallocate()'
       call msg(MSG_ERROR,VGD_PRFX//for_msg)  
       return
    endif
    status = VGD_OK
    return
  end function PRIV_reallocate4

  function PRIV_reallocate8(v,low,high) result(status)
    ! Allocate space for outputs
    real*8, dimension(:), pointer :: v          !vector to allocate
    integer, intent(in) :: low,high             !bounds for vector
    integer :: status                           !return status of function
    
    ! Internal variables
    integer :: check

    ! Deallocate and re-allocate with the correct size
    status = VGD_ERROR
    if (associated(v)) then
       deallocate(v,stat=check)
       if (check /= 0) then
          write(for_msg,*) 'in deallocate() from vgrid_genab_2001:reallocate()'
          call msg(MSG_ERROR,VGD_PRFX//for_msg) 
          return
       endif
    endif
    allocate(v(low:high),stat=check)
    if (check /= 0) then
       write(for_msg,*) 'in allocate() from vgrid_genab_2001::reallocate()'
       call msg(MSG_ERROR,VGD_PRFX//for_msg)        
       return
    endif
    status = VGD_OK
    return
  end function PRIV_reallocate8

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

  function PRIV_reallocateI(v,low,high) result(status)
    ! Allocate space for outputs
    integer, dimension(:), pointer :: v         !vector to allocate
    integer, intent(in) :: low,high             !bounds for vector
    integer :: status                           !return status of function
    
    ! Internal variables
    integer :: check

    ! Deallocate and re-allocate with the correct size
    status = VGD_ERROR
    if (associated(v)) then
       deallocate(v,stat=check)
       if (check /= 0) then
          write(for_msg,*) 'Error in deallocate() from vdescript_2001::reallocate()'
          call msg(MSG_ERROR,VGD_PRFX//for_msg)
          return
       endif
    endif
    allocate(v(low:high),stat=check)
    if (check /= 0) then
       write(for_msg,*) 'Error in allocate() from vdescript_2001::reallocate()'
       call msg(MSG_ERROR,VGD_PRFX//for_msg)
       return
    endif
    status = VGD_OK
    return
  end function PRIV_reallocateI

end module vdescript_2001

