program print_toctoc
   !
   use vGrid_Descriptors, only: vgrid_descriptor,vgd_new,vgd_print,vgd_get,VGD_ERROR
   !
   implicit none
   !
   type(vgrid_descriptor) :: vgd
   integer, parameter :: lu=10,ncle=5
   integer :: stat,npos,i
   integer :: fnom,fstouv,fstfrm,exdb,exfin,kind
   integer, dimension(:), pointer :: ip1s
   character(len=12), parameter :: version='v_1.1.0'
   character(len=256), dimension(ncle) :: cle,val,def
   logical :: ip1m_only_L, ip1t_only_L, box_L
   !
   !==========================================================================
   !
   ! Get keys
   cle=(/'fst.     ','ip1m_only','ip1t_only','kind    ','no_box   '/)
   val=(/'undef    ','no       ','no       ','undef   ','no       '/)
   def=(/'undef    ','yes      ','yes      ','undef   ','yes      '/)
   npos=1
   call ccard(cle,def,val,ncle,npos) 
   !
   ip1m_only_L=.false.
   ip1t_only_L=.false.
   box_L=.true.
   if(val(5).eq.'YES')box_L=.false.
   !
   if(trim(val(1)).eq.'undef')then
      stat=exdb('print_toctoc',version,'NON')
      print*,'Usage : print_toctoc -fst rpn_file'
      call exit(1)
   endif
   !
   if(trim(val(2)).eq.'YES')then
      ip1m_only_L=.true.
   endif
   if(trim(val(3)).eq.'YES')then
      ip1t_only_L=.true.
      if(ip1m_only_L.and.ip1t_only_L)then
         stat=exdb('print_toctoc',version,'NON')
         print*,'Chose on or the other -ip1m_only_L -ip1t_only_L'
         call exit(1)
      endif
   endif
   kind=-1
   if(trim(val(4)).ne.'undef')then
      read(val(4),*)kind
      print*,'Note : printing only level information for kind',kind
   endif
   !
   if(ip1m_only_L.or.ip1t_only_L)box_L=.false.
   !
   if(box_L)stat=exdb('print_toctoc',version,'NON')
   !
   !==========================================================================
   !
   stat=fnom(lu,val(1),"RND",0)
   if(stat.lt.0)then
      print*,'ERROR with fnom on',val(1)
      call exit(1)
   endif
   stat=fstouv(lu,'RND')
   if(stat.lt.0)then
      print*,'ERROR with fstouv on',val(1)
      call exit(1)
   endif
   !
   stat = vgd_new(vgd,lu,'fst',kind=kind)
   if(stat.eq.VGD_ERROR)then
      print*,'ERROR with vgd_new on',val(1)
      call exit(1)
   endif
   if(ip1m_only_L)then
      stat=vgd_get(vgd,'VIPM - level ip1 list (m)',ip1s)
      if(stat.eq.VGD_ERROR)then
         print*,'ERROR with vgd_get on','VIPM'
         call exit(1)
      endif
      do i=1,size(ip1s)
         print*,ip1s(i)
      enddo
   elseif(ip1t_only_L)then
      stat=vgd_get(vgd,'VIPT - level ip1 list (t)',ip1s)
      if(stat.eq.VGD_ERROR)then
         print*,'ERROR with vgd_get on','VIPT'
         call exit(1)
      endif
      do i=1,size(ip1s)
         print*,ip1s(i)
      enddo
   else
      stat = vgd_print(vgd)
      if(stat.eq.VGD_ERROR)then
         print*,'ERROR with vgd_print'
         call exit(1)
      endif
      if(box_L)stat=exfin('print_toctoc',version,'NON')
   endif
   !
   stat=fstfrm(lu)
   !
end program print_toctoc
